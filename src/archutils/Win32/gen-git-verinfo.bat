@echo off

:: Written by vyhd for OpenITG
:: Do whatever you'd like with this! I just hacked it together.
:: Appropriately, I make no guarantees to it functioning properly.

:: This is required to at least output a header with versioning info.
:: If it doesn't, the build will (correctly) fail to complete.

set BUILD_VERSION=unknown version
set BUILD_REV_DATE=unknown date
set BUILD_REV_TAG=unknown revision

:: Why I hate Windows, part 1: we have to use this incredibly ugly
:: and, frankly, nonsensical 'for' hack to assign output to variables
:: without using an intermediate temp file.

:: Why I hate Windows, part 2: we don't know if 'git' is an exe or
:: a bat, and if it's a bat, we'll never return from the initial call
:: unless we invoke it through 'call'. Compatibility!

:: Why I hate Windows, part 3: it's silently stripping out the = signs
:: in each invocation, so I spent at least half an hour trying to find
:: an alternative way to call --pretty="format:%ad" in a way that it
:: would accept. It turns out, if you use "--format=%%ad", the sign is
:: preserved. Go figure.

for /f %%i in ('call git tag') do set BUILD_VERSION=%%i
for /f %%i in ('call git log -1 "--format=%%ad" --date=short') do set BUILD_REV_DATE=%%i
for /f %%i in ('call git describe') do set BUILD_REV_TAG=%%i

if NOT "%BUILD_VERSION%" == "unknown version" (
	if NOT "%BUILD_VERSION%" == "%BUILD_REV_TAG%" (
		set BUILD_VERSION=%BUILD_VERSION% DEV
	)
)

echo Build version: %BUILD_VERSION%
echo Build revision date: %BUILD_REV_DATE%
echo Build revision tag: %BUILD_REV_TAG%

( 
echo /* Autogenerated by src/archutils/Win32/gen-git-verinfo.bat */
echo.
echo #define BUILD_VERSION	"%BUILD_VERSION%"
echo #define BUILD_DATE		"%BUILD_REV_DATE%"
echo #define BUILD_REVISION_TAG	"%BUILD_REV_TAG%"
) > verinfo.h